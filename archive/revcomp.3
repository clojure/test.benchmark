;;   The Computer Language Benchmarks Game
;;   http://benchmarksgame.alioth.debian.org/
;;
;; ported from Scala revcomp #8
;; contributed by Alex Miller

(ns alioth.revcomp
  (:gen-class)
  (:import [java.util.concurrent Executors Future Callable]))

(set! *warn-on-reflection* true)
(set! *unchecked-math* true)

(def bs (let [bs (byte-array 128)
              a "ACBDGHK\nMNSRUTWVYacbdghkmnsrutwvy"
              b "TGVHCDM\nKNSYAAWBRTGVHCDMKNSYAAWBR"
              c (.length a)]
          (loop [i 0]
            (when (< i c)
              (aset bs (.charAt a i) (byte (int (.charAt b i))))
              (recur (inc i))))
          bs))

(defn inner ^long [^long i ^long len ^bytes buf]
  (let [bs ^bytes bs]
    (if (< i len)
      (let [b (int (aget buf i))]
        (if (= b (int 62))  ;; >
          (inc i)
          (do
            (aset buf i (aget bs b))
            (recur (inc i) len buf))))
      i)))

(defn reverse-task [^bytes buf ^long begin ^long end-init]
  (fn []
    (loop [begin begin
           end end-init]
      (if (< begin end)
        (let [bb (aget buf begin)]
          (if (= bb 10)
            (recur (inc begin) end)
            (let [be (aget buf end)]
              (if (= be 10)
                (recur begin (dec end))
                (do
                  (aset buf begin be)
                  (aset buf end bb)
                  (recur (inc begin) (dec end)))))))
        end-init))))

(defn -main [& args]
  (let [pool (Executors/newFixedThreadPool (.availableProcessors (Runtime/getRuntime)))
        len (long (.available System/in))
        buf (byte-array len)]
    (.read System/in buf)
    (loop [i 0
           tasks []]
      (if (< i len)
        (let [b (long (aget buf i))]
          (if (= b 10)
            (let [j (inner i len buf)
                  end (- j 2)
                  task (reverse-task buf i end)
                  f (.submit pool ^Callable task)]
              (.execute pool task)
              (recur (inc j) (conj tasks f)))
            (recur (inc i) tasks)))
        (loop [i 0
               last 0]
          (if (< i (count tasks))
            (let [f ^Future (nth tasks i)
                  r (long @f)]
              (.write System/out buf last (- r last))
              (recur (inc i) r))
            (.write System/out buf last (- len last))))))
    (.shutdown pool)))
